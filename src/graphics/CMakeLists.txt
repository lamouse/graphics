set(CMAKE_INCLUDE_CURRENT_DIR ON)
set(PROGRAM_NAME "graphics")
set(QML_MODULE_NAME "graphics")
set(QML_IMPORT_PATH "${CMAKE_BINARY_DIR}/qml" CACHE STRING "QML import path")

include(${CMAKE_CURRENT_SOURCE_DIR}/graphics.cmake)

if(USE_QT AND TARGET Qt6::Quick)
    add_subdirectory(ui)
    qt_standard_project_setup(REQUIRES 6.8)
    qt_add_executable(${PROGRAM_NAME} ${sources})
    set_target_properties(${PROGRAM_NAME} PROPERTIES
        AUTOUIC ON
        AUTOMOC ON
        AUTORCC ON
    )
    qt_add_qml_module(${PROGRAM_NAME}
        URI ${QML_MODULE_NAME}
        VERSION 1.0
        QML_FILES
        Main.qml
        SOURCES
        ui/render_config.hpp
        ui/render_config.cpp
    )
    set_target_properties(${PROGRAM_NAME} PROPERTIES
        WIN32_EXECUTABLE TRUE
    )
else()
    add_executable(${PROGRAM_NAME} ${sources})
endif()

add_dependencies(${PROGRAM_NAME} copy_fonts)

if(USE_QT)
    target_compile_definitions(${PROGRAM_NAME} PRIVATE USE_QT)

    if(WIN32 AND NOT CMAKE_CROSSCOMPILING)
        # Ëé∑Âèñ Qt6 ÂÆâË£ÖÂâçÁºÄÔºàvcpkg ‰ºöËÆæÁΩÆ Qt6_DIRÔºâ
        get_target_property(_qt6_core_location Qt6::Core LOCATION)
        get_filename_component(_qt6_bin_dir "${_qt6_core_location}" DIRECTORY)
        get_filename_component(_qt6_prefix "${_qt6_bin_dir}/.." ABSOLUTE)
        set(QT6_PLUGINS_DIR "${_qt6_prefix}/${QT6_INSTALL_PLUGINS}/platforms/qwindows.dll")
        get_target_property(_windeployqt_exe Qt6::windeployqt IMPORTED_LOCATION)

        if(CMAKE_BUILD_TYPE STREQUAL "Debug")
            if(USE_VCPKG)
                set(QT6_PLUGINS_DIR "${_qt6_prefix}/debug/${QT6_INSTALL_PLUGINS}/platforms/qwindowsd.dll")
                string(REPLACE "windeployqt.exe" "windeployqt.debug.bat" _windeployqt_exe "${_windeployqt_exe}")
            endif()
        endif()

        # ÊûÑÂª∫ÂêéÂ§çÂà∂Âπ≥Âè∞Êèí‰ª∂ qwindows.dll
        add_custom_command(TARGET ${PROGRAM_NAME} POST_BUILD
            COMMAND ${CMAKE_COMMAND} -E make_directory "$<TARGET_FILE_DIR:${PROGRAM_NAME}>/plugins/platforms"
            COMMAND ${_windeployqt_exe}
            --include-soft-plugins
            --qmldir ${CMAKE_CURRENT_SOURCE_DIR}
            --no-translations
            --no-libraries
            --skip-plugin-types networkinformation,imageformats,iconengines,qmltooling,styles,tls,generic
            "$<TARGET_FILE_DIR:${PROGRAM_NAME}>"
            COMMAND ${CMAKE_COMMAND} -E copy_if_different
            ${QT6_PLUGINS_DIR}
            "$<TARGET_FILE_DIR:${PROGRAM_NAME}>/plugins/platforms/"
            COMMENT "Copying Qt platform plugin qwindows.dll..."
        )
    endif()

    if(TARGET Qt6::Quick)
        target_link_libraries(${PROGRAM_NAME} PRIVATE RenderCtrlLibplugin)
    endif()

    target_link_libraries(${PROGRAM_NAME} PRIVATE Qt6::Widgets)

else()
    target_compile_definitions(${PROGRAM_NAME} PRIVATE USE_SDL)
endif()

target_link_libraries(${PROGRAM_NAME} PRIVATE core utils render-core effects resource)
target_link_libraries(${PROGRAM_NAME} PRIVATE SDL3::SDL3 world system Imgui::Imgui ImGuizmo::ImGuizmo)

target_link_directories(${PROGRAM_NAME} PRIVATE ${CMAKE_BINARY_DIR}/lib)

add_custom_command(TARGET ${PROGRAM_NAME} POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
    $<TARGET_RUNTIME_DLLS:${PROGRAM_NAME}>
    $<TARGET_FILE_DIR:${PROGRAM_NAME}>
    COMMAND_EXPAND_LISTS
)

if(LINUX)
    target_link_libraries(
        ${PROGRAM_NAME} PRIVATE
        X11 # üî¥ ÂøÖÈ°ªÔºÅËß£ÂÜ≥ XInternAtom, XChangeProperty Á≠â
        Xrandr # Êé®ËçêÔºöÂ§öÂ±èÊîØÊåÅ
        Xinerama # Êé®ËçêÔºöËôöÊãüÊ°åÈù¢ÊãºÊé•Ê£ÄÊµã
        Xcursor # Êé®ËçêÔºöÈº†Ê†áÂÖâÊ†á
        Xxf86vm # Êé®ËçêÔºöÂÖ®Â±èÂàáÊç¢
        Xi # Êé®ËçêÔºöËæìÂÖ•ËÆæÂ§áÊâ©Â±ï
    )
endif()

# Ê∑ªÂä†ÁîüÊàêÁöÑ‰ª£Á†ÅÁõÆÂΩï
include_directories(${CMAKE_BINARY_DIR}/generated)

if(MSVC)
    set_target_properties(${PROGRAM_NAME} PROPERTIES LINK_FLAGS_RELEASE "/SUBSYSTEM:WINDOWS")
elseif(MINGW)
    set_target_properties(${PROGRAM_NAME} PROPERTIES LINK_FLAGS_RELEASE "-Wl,--subsystem,windows")
endif()